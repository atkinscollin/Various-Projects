color(1,6,1). color(1,8,5). color(1,9,9).
color(2,2,3). color(2,5,8). color(2,8,6).
color(3,2,5). color(3,4,9). color(3,7,2). color(3,9,3).
color(4,3,6). color(4,4,7). color(4,5,5). color(4,6,2).
color(5,2,1). color(5,5,9). color(5,8,2).
color(6,1,7). color(6,4,1). color(6,5,6). color(6,6,3).
color(7,1,1). color(7,3,9). color(7,6,6). color(7,8,4).
color(8,2,6). color(8,5,2). color(8,7,8).
color(9,1,2). color(9,2,4). color(9,4,3).

% First describe the geometry
coord(1..9).
cell(R,C) :- coord(R), coord(C).
sameCell(R,C,R,C) :- cell(R,C).
sameRow(R,C1,R,C2) :- cell(R,C1), cell(R,C2).
sameClmn(R1,C,R2,C) :- cell(R1,C), cell(R2,C).
sameBlock(R1,C1,R2,C2)
:- cell(R1,C1), cell(R2,C2),
(R1-1)/3 = (R2-1)/3, (C1-1)/3 = (C2-1)/3.

% Now describe properties of the coloring
label(1..9).
1 { color(R,C,L): label(L) } 1 :- cell(R,C).
:- color(R1,C1,L), color(R2,C2,L), sameRow(R1,C1,R2,C2),
not sameCell(R1,C1,R2,C2).
:- color(R1,C1,L), color(R2,C2,L), sameClmn(R1,C1,R2,C2),
not sameCell(R1,C1,R2,C2).
:- color(R1,C1,L), color(R2,C2,L), sameBlock(R1,C1,R2,C2),

not sameCell(R1,C1,R2,C2).

#show color/3.